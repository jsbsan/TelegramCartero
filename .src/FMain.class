' Gambas class file

'
' TelegramCartero
' Envio de mensajes por telegram a una lista de personas elegidas
'
' Copyright (C) Julio Sanchez Berro (jsbsan)
'
' This program is free software; you can redistribute it and/or modify
' it under the terms of the GNU General Public License as published by
' the Free Software Foundation; either version 2 of the License, or
' (at your option) any later version.
'
' This program is distributed in the hope that it will be useful,
' but WITHOUT ANY WARRANTY; without even the implied warranty of
' MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
' GNU General Public License for more details.
'
' You should have received a copy of the GNU General Public License
' along with this program; if not, write to the Free Software
' Foundation, Inc., 51 Franklin St, Fifth Floor,
' Boston, MA  02110-1301  USA
'

Public banderaFinContactos As Boolean = False

Public Content As String

Public EscribirEnPrograma As Stream

Public Sub Form_Open()

  tituloVentanaPrincipal()
  Labelversion.text = Application.Version

  If Not Telegram.buscarProgramaTelegram() Then
    Message.Info(("No es posible encontrar el ejecutable: telegram-cli\nEl programa no funcionara... :("))
    Me.Close()
  Endif

  'define gridviews
  Comun.definirGridviewDatos(GridViewContactos)
  'listado de contactos
  'Telegram.listaContactos(GridViewContactos)-> cargaba lo usuarios de contact_list

  CargoGrupoCombobox()
  ComboBoxGrupos_Click()

End

Public Sub agregalistbox(contacto As String)
  'El siguiente codigo añade un contacto a la lista de envio, solo si no esta ya incluido.

  If ListBoxContactosEnvio.list.Find(contacto) = -1 Then

    ListBoxContactosEnvio.Add(contacto)
    ListBoxContactosEnvio.Refresh()
  Else

    Message.Info(("Conctacto ya añadido:") & " " & contacto)

  Endif

End

Public Sub ButtonAgregar_Click()

  Dim a As Integer

  For a = 0 To GridViewContactos.Rows.Selection.Max

    agregaListbox(GridViewContactos[GridViewContactos.Rows.Selection[a], 0].text)

  Next

End

Public Sub ButtonQuitar_Click()

  Dim a As Integer

  If ListBoxContactosEnvio.index <> -1 Then

    Print ListBoxContactosEnvio.index

    ListBoxContactosEnvio.Remove(ListBoxContactosEnvio.index)

  Endif

End

Public Sub GridViewContactos_DblClick()

  'veo si esta elegido alguna fila, si es asi, la paso al listabox
  If GridViewContactos.row = -1 Then
    Message.Info(("Debe seleccionar un contacto"))
    Return
  Endif

  agregaListbox(GridViewContactos[GridViewContactos.row, 0].text)

End

Public Sub ButtonEnviar_Click()

  Dim comando As String
  Dim salida As String

  Dim a As Integer
  'enviar mensaje a varias contactos

  If ListBoxContactosEnvio.List.count = 0 Then

    Message.Info(("No hay ningun contacto a enviar mensaje"))
    Return

  Endif

  If ListBoxContactosEnvio.list.count = 1 Then

    ' comando = ruta & " " & "-k tg-server.pub msg " & Replace(ListBoxContactosEnvio.List[0], " ", "_") & " \"" & TextAreaMensaje.text & "\""
    ' Shell comando
    EscribirEnPrograma = Exec [Telegram.ruta, "-k", "tg-server.pub"] For Write

    escribe(ListBoxContactosEnvio.List[0], TextAreaMensaje.text)

    Message.Info(("mensaje enviado"))
    EscribirEnPrograma.Close

    Return
  Endif

  'Hay mas de un contacto a que enviar:
  escribeInformacionDelEnvio(0)

  Inc Application.Busy
  ButtonEnviar.Enabled = False
  'escribo el primer mensaje...
  EscribirEnPrograma = Exec [Telegram.ruta, "-k", "tg-server.pub"] For Write

  escribe(ListBoxContactosEnvio.List[0], TextAreaMensaje.text)

  For a = 1 To ListBoxContactosEnvio.List.Count - 1
    Wait 10 'para que no me considere spamm
    EscribirEnPrograma = Exec [Telegram.ruta, "-k", "tg-server.pub"] For Write

    escribe(ListBoxContactosEnvio.List[a], TextAreaMensaje.text)

    escribeInformacionDelEnvio(a)
    Me.Title = ("enviando...") & Str$(a + 1) & " " & ("de") & " " & Str$(ListBoxContactosEnvio.List.Count)
    ButtonEnviar.Text = ("enviando...") & Str$(a + 1) & " " & ("de") & " " & Str$(ListBoxContactosEnvio.List.Count)

  Next

  ButtonEnviar.Enabled = True
  ButtonEnviar.Text = ("Enviar")
  tituloVentanaPrincipal()

  Dec Application.Busy
  'cerrar y matar el proceso
  EscribirEnPrograma.Close

  Message.Info(("Mensajes enviados"))

End

Public Sub EscribeInformacionDelEnvio(a As Integer)
  'Modifica titulo del formulario

  Me.Title = ("enviando...") & Str$(a + 1) & " " & ("de") & " " & Str$(ListBoxContactosEnvio.List.Count)
  'Modifica titulo del boton
  ButtonEnviar.Text = ("enviando...") & Str$(a + 1) & " " & ("de") & " " & Str$(ListBoxContactosEnvio.List.Count)
  ButtonEnviar.refresh()

End

Public Sub escribe(contacto As String, mensaje As String)
  'escribir linea de contacto si funciona y lo tengo que hace para que reconozca a los que tengoque enviar el mensaje !!!!

  Write #EscribirEnPrograma, "contact_list" & GB.Lf
  Wait 1
  'eliminar del mensaje los retornos de carro...lo sustituyo por un punto
  mensaje = Replace(mensaje, gb.lf, ".")
  Wait 1
  Write #EscribirEnPrograma, "msg " & Replace(Replace(contacto, " ", "_"), " ", "") & " \"" & mensaje & "\"" & GB.Lf

End

Public Sub tituloVentanaPrincipal()

  Me.Title = ("Cartero Telegram")

End

Public Sub PictureBox1_MouseDown()

  FormGrupos.ShowModal()
  CargoGrupoCombobox()
  ComboBoxGrupos_Click()

End

'--------------------------------------
'cargar datos de grupos y contactos...
'--------------------------------------
Public listaGrupos As String[]
Public GrupoActual As String
Public contenido As New Collection

Public Sub CargoGrupoCombobox() 'cargo el listado de grupos y creo la variable contenido que continee grupo y su usuarios

  Dim g As String 'grupo temporal
  Dim usu As String[] 'lista de usuarios que pertenecen a un grupo

  listaGrupos = Settings["ListaGrupos", ["General"]]

  GrupoActual = "General"

  contenido = New Collection

  ComboBoxGrupos.Clear()

  For Each g In listaGrupos
    ComboBoxGrupos.Add(g) 'añade contacto
    usu = Settings[g] 'lee de la configuracion los usuarios
    If Not IsNull(usu) Then
      contenido.Add(usu, g) 'los cargo en memoria...
    Endif
  Next

End

Public Sub ComboBoxGrupos_Click()

  Print "  cambio de elegido..", ComboBoxGrupos.text

  'recargo los datos de usuarios del grupo elegido..

  recargarGridUsuarios(contenido[ComboBoxGrupos.text])

End

Public Sub recargarGridUsuarios(cadena As String[])

  Dim usu As String

  GridViewContactos.Rows.count = 0 'vacio el contenido del gridview

  If Not IsNull(cadena) Then
    For Each usu In cadena
      GridViewContactos.Rows.count += 1
      GridViewContactos[GridViewContactos.rows.max, 0].text = usu
    Next
  Endif

End

Public Sub Label1_MouseDown()

  Desktop.Open("http://sanpedroapostol.org/")

End

Public Sub LabelGrupo_MouseDown()

  PictureBox1_MouseDown()

End
